@top Program { (expression | Cmd )* }

expression {
  Command |
  Repeat |
  IfThen |
  CustomRef
}

Command { 
  @specialize<idx, "Schritt"> | 
  @specialize<idx, "LinksDrehen"> |
  @specialize<idx, "RechtsDrehen"> |
  @specialize<idx, "Hinlegen"> |
  @specialize<idx, "Aufheben"> |
  @specialize<idx, "MarkeSetzen"> |
  @specialize<idx, "MarkeLöschen"> |
  @specialize<idx, "Beenden"> |
  @specialize<idx, "Unterbrechen">
}

Repeat {
  RepeatStart ((RepeatWhileKey Condition?) | (Times RepeatTimesKey)) expression* RepeatEnd
}

RepeatStart[closedBy="RepeatEnd"] { @specialize<idx, "wiederhole"> }

RepeatEnd { @specialize<idx, "endewiederhole"> }

RepeatWhileKey { @specialize<idx, "solange"> }

RepeatTimesKey { @specialize<idx, "mal"> }

Times { number }

Condition {
  @specialize<idx, "IstWand"> |
  @specialize<idx, "NichtIstWand"> |
  @specialize<idx, "IstZiegel"> |
  @specialize<idx, "NichtIstZiegel"> |
  @specialize<idx, "IstMarke"> |
  @specialize<idx, "NichtIstMarke">
}

IfThen {
  IfKey Condition? ThenKey expression* (ElseKey expression*)? IfEndKey
}

IfKey[closedBy="IfEndKey"] { @specialize<idx, "wenn"> }

ThenKey { @specialize<idx, "dann"> }

IfEndKey { @specialize<idx, "endewenn"> }

ElseKey { @specialize<idx, "sonst"> }

Cmd {
  CmdStart CmdName expression* CmdEnd
}

CmdName { idx }

CmdStart[closedBy="CmdEnd"] { @specialize<idx, "Anweisung"> }

CmdEnd { @specialize<idx, "endeAnweisung"> }

CustomRef { idx }


@tokens {
  whitespace { $[ \n\r\t]+ }

  number { $[0-9]+ }

  Comment { "{" ![}]* "}"? }

  idx { $[A-Za-zäüöÄÜÖß_] $[0-9A-Za-zäüöÄÜÖß_]* }
}

@skip { Comment | whitespace }