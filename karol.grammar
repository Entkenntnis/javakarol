@top Program { (expression | Cmd | Cond)* }

expression {
  Command |
  SpecialCommand |
  Repeat |
  IfThen |
  TF |
  CustomRef
}

Command { 
  @specialize<idx, "Schritt"> | 
  @specialize<idx, "LinksDrehen"> |
  @specialize<idx, "RechtsDrehen"> |
  @specialize<idx, "Hinlegen"> |
  @specialize<idx, "Aufheben"> |
  @specialize<idx, "MarkeSetzen"> |
  @specialize<idx, "MarkeLöschen"> |
  @specialize<idx, "Ton"> |
  @specialize<idx, "Beenden"> |
  @specialize<idx, "Warten">
}

SpecialCommand {
  @specialize<idx, "schnell"> |
  @specialize<idx, "langsam">
}

Repeat {
  RepeatStart ((RepeatWhileKey Condition?) | (Times RepeatTimesKey)) expression* RepeatEnd
}

RepeatStart[closedBy="RepeatEnd"] { @specialize<idx, "wiederhole"> }

RepeatEnd { @specialize<idx, "endewiederhole"> }

RepeatWhileKey { @specialize<idx, "solange"> }

RepeatTimesKey { @specialize<idx, "mal"> }

Times { number }

Condition {
  Not?
  (@specialize<idx, "IstWand"> |
  @specialize<idx, "NichtIstWand"> |
  @specialize<idx, "IstZiegel"> |
  @specialize<idx, "NichtIstZiegel"> |
  @specialize<idx, "IstMarke"> |
  @specialize<idx, "NichtIstMarke"> |
  @specialize<idx, "IstOsten"> |
  @specialize<idx, "IstSüden"> |
  @specialize<idx, "IstWesten"> |
  @specialize<idx, "IstNorden">)
}

Not { @specialize<idx, "nicht"> }

IfThen {
  IfKey Condition? ThenKey expression* (ElseKey expression*)? IfEndKey
}

IfKey[closedBy="IfEndKey"] { @specialize<idx, "wenn"> }

ThenKey { @specialize<idx, "dann"> }

IfEndKey { @specialize<idx, "endewenn"> }

ElseKey { @specialize<idx, "sonst"> }

Cmd {
  CmdStart CmdName expression* CmdEnd
}

CmdName { idx }

CmdStart[closedBy="CmdEnd"] { @specialize<idx, "Anweisung"> }

CmdEnd { @specialize<idx, "endeAnweisung"> }

Cond {
  CondStart CondName expression* CondEnd
}

CondName { idxExt }

CondStart[closedBy="CondEnd"] { @specialize<idx, "Bedingung"> }

CondEnd { @specialize<idx, "endeBedingung"> }

TF {
  @specialize<idx, "wahr"> |
  @specialize<idx, "falsch">
}

CustomRef { idx}

@tokens {
  whitespace { $[ \n\r\t]+ }

  number { $[0-9]+ }

  Comment { "{" ![}]* "}" }

  idx { $[A-Za-z_] $[0-9A-Za-z_]* }

  idxExt { $[A-Za-z_] $[0-9A-Za-z_()]* }
}

@skip { Comment | whitespace }